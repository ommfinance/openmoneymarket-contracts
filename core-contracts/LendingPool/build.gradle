version = '0.0.2'

dependencies {
    implementation project(':AddressProvider')
    implementation project(':Score')
    implementation project(':ScoreUtils')
    implementation project(':OMMStruct')

    testImplementation project(':TestUtils')

    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.6.1'
}

optimizedJar {
    mainClassName = 'finance.omm.score.core.lendingpool.LendingPoolImpl'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    enableDebug = false
}

deployJar {
    endpoints {
        sejong {
            uri = 'https://sejong.net.solidwallet.io/api/v3'
            to = rootProject.findProperty("contracts.lending-pool")
            nid = 0x53
        }
        berlin {
            uri = 'https://berlin.net.solidwallet.io/api/v3'
            to = rootProject.findProperty("contracts.lending-pool")
            nid = 0x7
        }
        mainnet {
            uri = 'https://ctz.solidwallet.io/api/v3'
            nid = 0x1
        }
    }
    keystore = rootProject.hasProperty('keystoreName') ? "$keystoreName" : ''
    password = rootProject.hasProperty('keystorePass') ? "$keystorePass" : ''
    parameters {
        arg('addressProvider', rootProject.findProperty("contracts.address-provider") as String)
    }
}

test {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    useJUnitPlatform()

    rootProject.allprojects {
        if (it.getTasks().findByName('optimizedJar')) {
            dependsOn(it.getTasks().getByName('optimizedJar'))
        }
    }

    options {
        testLogging.showStandardStreams = true
        description = 'Runs integration tests.'
        group = 'verification'
        testClassesDirs = sourceSets.intTest.output.classesDirs
        classpath = sourceSets.intTest.runtimeClasspath
    }
}

